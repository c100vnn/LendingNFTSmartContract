// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract OwnableContract {
    address public owner;
    address public pendingOwner;
    address public admin;

    event NewAdmin(address oldAdmin, address newAdmin);
    event NewOwner(address oldOwner, address newOwner);
    event NewPendingOwner(address oldPendingOwner, address newPendingOwner);

    function initOwnableContract(address _owner, address _admin) internal {
        owner = _owner;
        admin = _admin;
    }

    function _onlyOwner() private view {
        require(msg.sender == owner, 'onlyOwner');
    }

    modifier onlyOwner() {
        _onlyOwner();
        _;
    }

    function _onlyPendingOwner() private view {
        require(msg.sender == pendingOwner, 'onlyPendingOwner');
    }

    modifier onlyPendingOwner() {
        _onlyPendingOwner();
        _;
    }

    function _onlyAdmin() private view {
        require(msg.sender == admin || msg.sender == owner, 'onlyAdmin');
    }

    modifier onlyAdmin() {
        _onlyAdmin();
        _;
    }

    function transferOwnership(address _pendingOwner) public onlyOwner {
        emit NewPendingOwner(pendingOwner, _pendingOwner);
        pendingOwner = _pendingOwner;
    }

    function renounceOwnership() public virtual onlyOwner {
        emit NewOwner(owner, address(0));
        emit NewAdmin(admin, address(0));
        emit NewPendingOwner(pendingOwner, address(0));

        owner = address(0);
        pendingOwner = address(0);
        admin = address(0);
    }

    function acceptOwner() public onlyPendingOwner {
        emit NewOwner(owner, pendingOwner);
        owner = pendingOwner;

        address newPendingOwner = address(0);
        emit NewPendingOwner(pendingOwner, newPendingOwner);
        pendingOwner = newPendingOwner;
    }

    function setAdmin(address newAdmin) public onlyOwner {
        emit NewAdmin(admin, newAdmin);
        admin = newAdmin;
    }
}
